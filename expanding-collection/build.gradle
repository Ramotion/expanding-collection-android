apply plugin: 'com.android.library'
apply plugin: 'signing'
apply plugin: 'com.bmuschko.nexus'
apply plugin: "jacoco"

group = 'com.ramotion.expandingcollection'
version = '0.9.0'

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 1
        versionName version

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            versionNameSuffix " Debug"
            debuggable true
            testCoverageEnabled = true
        }
    }
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_7
        sourceCompatibility JavaVersion.VERSION_1_7
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    productFlavors {
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'com.android.support:appcompat-v7:28.0.0'
    testImplementation 'junit:junit:4.12'
}

modifyPom {
    project {
        name 'Expanding Collection for Android'
        description 'ExpandingCollection is a card peek/pop controller https://business.ramotion.com'
        url 'https://github.com/Ramotion/expanding-collection-android'
        inceptionYear '2017'

        scm {
            url 'https://github.com/Ramotion/expanding-collection-android'
            connection 'scm:git@github.com:Ramotion/expanding-collection-android.git'
            developerConnection 'scm:git@github.com:Ramotion/expanding-collection-android.git'
        }

        licenses {
            license {
                name 'The MIT License (MIT)'
                url 'https://opensource.org/licenses/mit-license.php'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'oleg-vasiliev'
                name 'Oleg Vasiliev'
                email 'oleg-vasiliev@hotmail.com'
            }
        }
    }
}

extraArchive {
    sources = false
    tests = false
    javadoc = false
}

nexus {
    sign = true
    repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
    snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
}

jacoco {
    toolVersion = "0.7.6.201602180812"
    reportsDir = file("$buildDir/reports/jacoco")

}

task jacocoReport(type: JacocoReport, dependsOn: 'testDebugUnitTest') {
    group = 'Reporting'
    description = 'Generate Jacoco coverage reports after running tests.'

    reports {
        xml.enabled = true
        html.enabled = true
    }

    classDirectories = fileTree(
            dir: 'build/intermediates/classes/debug',
            excludes: [
                    '**/R*.class',
                    '**/BuildConfig*'
            ]
    )

    sourceDirectories = files('src/main/java')
    executionData = files('build/jacoco/testDebugUnitTest.exec')

    doFirst {
        files('build/intermediates/classes/debug').getFiles().each { file ->
            if (file.name.contains('$$')) {
                file.renameTo(file.path.replace('$$', '$'))
            }
        }
    }
}